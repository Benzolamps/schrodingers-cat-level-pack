# string constants
string Code_TS_DoorAString "TTRS:ScLevelPack.CodeTS.DoorA=Door A"
string Code_TS_DoorBString "TTRS:ScLevelPack.CodeTS.DoorB=Door B"
string Code_TS_DoorCString "TTRS:ScLevelPack.CodeTS.DoorC=Door C"

string Code_TS_DoorUnlockedString [[TTRS:ScLevelPack.CodeTS.DoorUnlocked= has been unlocked.]]

string Code_TS_UnlockDoor "TTRS:ScLevelPack.CodeTS.UnlockDoor=Unlock "

string Code_TS_DoorEnterCodeString [[TTRS:ScLevelPack.CodeTS.DoorEnterCode= is protected with a code.%w9
Please enter the code:%w9]]

# add options to menu
player when (MenuOptions and Code_TS_Stage < 4) {
  options: {
    "TTRS:notrans=open device_manager.exe"
    short: "TTRS:ScLevelPack.CodeTS.DeviceManager=[Device Manager]"
    next: Code_TS_List
  }
}

# show unlock door options
terminal when (Code_TS_List) {
  prompt: "%t'CommonPrompt'"
  next: Code_TS_UnlockDoorOptions
}

# show code read
player when (Code_TS_UnlockDoorOptions and Code_TS_CodeRead == 1) {
  options: {
    "TTRS:notrans=open code.txt"
    short: "TTRS:ScLevelPack.CodeTS.ReadCode=Read code from previous sessions"
    next: Code_TS_ShowCode
  }
}

# unlock Door A option
player when (Code_TS_UnlockDoorOptions and Code_TS_Stage > 0) {
  options: { "%t'Code_TS_UnlockDoor'%t'Code_TS_DoorAString'" next: Code_TS_UnlockA }
}

# unlock Door B option
player when (Code_TS_UnlockDoorOptions and Code_TS_Stage > 1) {
  options: { "%t'Code_TS_UnlockDoor'%t'Code_TS_DoorBString'" next: Code_TS_UnlockB }
}

# unlock Door C option
player when (Code_TS_UnlockDoorOptions and Code_TS_Stage > 2) {
  options: { "%t'Code_TS_UnlockDoor'%t'Code_TS_DoorCString'" next: Code_TS_UnlockC }
}

# exit option
player when (Code_TS_UnlockDoorOptions) {
  options: { "TTRS:ScLevelPack.Exit=Exit" next: Menu }
}

# show code read (Door A, Door B)
terminal when (Code_TS_ShowCode and Code_TS_Stage < 3) {
  text: [[TTRS:notrans=
▓▓▓▓▓▓▓▓▓▓▓▓%w9
▓▓ %o"Code_TS_Code" ▓▓%w9
▓▓▓▓▓▓▓▓▓▓▓▓%w9

]]
  goto: Code_TS_List
}

# show code read (Door C, hide 3th code)
terminal when (Code_TS_ShowCode and Code_TS_Stage == 3) {
  text: [[TTRS:notrans=
▓▓▓▓▓▓▓▓▓▓▓▓%w9
▓▓ %O'Code_TS_CodePrefix'█%o'Code_TS_CodeSuffix' ▓▓%w9
▓▓▓▓▓▓▓▓▓▓▓▓%w9

]]
  goto: Code_TS_List
}

# Door A unlocked
terminal when (Code_TS_UnlockA and Code_TS_Stage > 1) {
  text: [[TTRS:notrans=
%t'Code_TS_DoorAString'%t'Code_TS_DoorUnlockedString'

]]
  goto: Code_TS_List
}

# Door B unlocked
terminal when (Code_TS_UnlockB and Code_TS_Stage > 2) {
  text: [[TTRS:notrans=
%t'Code_TS_DoorBString'%t'Code_TS_DoorUnlockedString'

]]
  goto: Code_TS_List
}

# Door C unlocked
terminal when (Code_TS_UnlockC and Code_TS_Stage > 3) {
  text: [[TTRS:notrans=
%t'Code_TS_DoorCString'%t'Code_TS_DoorUnlockedString'

]]
  goto: Code_TS_List
}

# Door A enter code
terminal when (Code_TS_UnlockA and Code_TS_Stage == 1) {
  text: [[TTRS:notrans=
%t'Code_TS_DoorAString'%t'Code_TS_DoorEnterCodeString'

]]
}

# Door B enter code
terminal when (Code_TS_UnlockB and Code_TS_Stage == 2) {
  text: [[TTRS:notrans=
%t'Code_TS_DoorBString'%t'Code_TS_DoorEnterCodeString'

]]
}

# Door C enter code
terminal when (Code_TS_UnlockC and Code_TS_Stage == 3) {
  text: [[TTRS:notrans=
%t'Code_TS_DoorCString'%t'Code_TS_DoorEnterCodeString'

]]
}

# start enter code
terminal when (Code_TS_UnlockA or Code_TS_UnlockB or Code_TS_UnlockC) {
  prompt: "%t'CommonPrompt'"
  setlocal: Code_TS_Attempts;=3;
  goto: Code_TS_EnterCode
}

# enter code input area
terminal when (Code_TS_EnterCode) {
  enter_code: Code_TS_Code Code_TS_CodeOK Code_TS_CodeWrong
}

# time switch not recording, code accepted
terminal when (Code_TS_CodeOK and not Code_TS_Recording) {
  text: [[TTRS:ScLevelPack.CodeTS.CodeUnlocked=
Code accepted.%w9
Unlocking.%w3.%w3.%w3.%w3.%w3%e7
Done.%w9

]]
}

# time switch recording, code accepted
terminal when (Code_TS_CodeOK and Code_TS_Recording) {
  text: [[TTRS:ScLevelPack.CodeTS.CodeUnlockFailed=
Code accepted.%w9
Unlocking.%w3.%w3.%w3.%w3.%w3%s0
Failed.%w3 Account has been frozen!%w9

]]
}

# after code accepted exit
terminal when (Code_TS_CodeOK) {
  prompt: "%t'CommonPrompt'"
  setlocal: Code_TS_Attempts;=3;
  options: { "TTRS:ScLevelPack.Exit=Exit" next: Exit }
}

# code incorrect show attempts
terminal when (Code_TS_CodeWrong and Code_TS_Attempts > 1) {
  setlocal: Code_TS_Attempts;-=1;
  text: [[TTRS:ScLevelPack.CodeTS.CodeAttempts=
Code incorrect!%s0%w9
Try Again (%O'Code_TS_Attempts' more tries left).%w9
Please enter the code:%w9

]]
}

# retry enter code
terminal when (Code_TS_CodeWrong and Code_TS_Attempts > 1) {
  prompt: "%t'CommonPrompt'"
  goto: Code_TS_EnterCode
}

# code incorrect failed attempted
terminal when (Code_TS_CodeWrong and Code_TS_Attempts == 1) {
  text: [[TTRS:ScLevelPack.CodeTS.CodeFailed=
Code incorrect!%s0%w9
Too many failed attempts.%w9
Exiting session.%w9

]]
}

# hack it
terminal when (Code_TS_CodeWrong and Code_TS_Attempts == 1) {
  prompt: "%t'CommonPrompt'"
  goto: Code_TS_Hack
}

# Door A hack it options
terminal when (Code_TS_Hack and Code_TS_Stage == 1) {
  options: {
    "TTRS:notrans=SELECT code FROM sys_dev WHERE sn = 'A'" short: "TTRS:ScLevelPack.CodeTS.HackIt=Hack it" next: Code_TS_GetCode
  }
}

# Door B hack it options
terminal when (Code_TS_Hack and Code_TS_Stage == 2) {
  options: {
    "TTRS:notrans=SELECT code FROM sys_dev WHERE sn = 'B'" short: "TTRS:ScLevelPack.CodeTS.HackIt=Hack it" next: Code_TS_GetCode
  }
}

# Door C hack it options
terminal when (Code_TS_Hack and Code_TS_Stage == 3) {
  options: {
    "TTRS:notrans=SELECT code FROM sys_dev WHERE sn = 'C'" short: "TTRS:ScLevelPack.CodeTS.HackIt=Hack it" next: Code_TS_GetCode
  }
}

# querying database
terminal when (Code_TS_GetCode) {
  text: [[TTRS:ScLevelPack.CodeTS.HackQuery=
Querying.%w9.%w9.%w9.%w9.%w9
Done.%w9
]]
}

# show result code (Door A, Door B)
terminal when (Code_TS_GetCode and Code_TS_Stage < 3) {
  text: [[TTRS:notrans=
▓▓▓▓▓▓▓▓▓▓▓▓%w9
▓▓ %o"Code_TS_Code" ▓▓%w9
▓▓▓▓▓▓▓▓▓▓▓▓%w9
]]
}

# show result code (Door C, show only the first two)
terminal when (Code_TS_GetCode and Code_TS_Stage == 3) {
  text: [[TTRS:notrans=
▓▓▓▓▓▓▓▓▓▓▓▓%w9
▓▓ %O'Code_TS_CodePrefix'%w9%w9%s0████ ]]
}

# Door C connection lost
terminal when (Code_TS_GetCode and Code_TS_Stage == 3) {
  text: [[TTRS:ScLevelPack.CodeTS.ConnectionLost=※※ection lost!%w9
]]
}

# code reset (Door A, reset failed)
terminal when (Code_TS_GetCode and Code_TS_Stage == 1) {
  text: [[TTRS:ScLevelPack.CodeTS.ResetFailed=
WARNING: Illegal user access detected!%w9
Resetting code.%w3.%w3.%w3.%w3.%w3%w9%s0
Failed.%w9%e6

]]
}

# code reset (Door B, Door C, reset done)
terminal when (Code_TS_GetCode and Code_TS_Stage > 1) {
  text: [[TTRS:ScLevelPack.CodeTS.ResetFinish=
WARNING: Illegal user access detected!%w9
Resetting code.%w3.%w3.%w3.%w3.%w3%w9
Done.%w9%e6

]]
}

# after code reset exit
terminal when (Code_TS_GetCode) {
  prompt: "%t'CommonPrompt'"
  options: { "TTRS:ScLevelPack.Exit=Exit" next: Exit }
}

# load main dialog
include "Content/Talos/Levels/sc_level_pack/Dialogs/MainDialog.dlg"
